#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ВыводИмеющихсяАктовВСписокДоговоров();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьАкты(Команда)
	
	ДлительнаяОперация = СоздатьАктыНаСервере();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ТекстСообщения = "Формирование актов ...";
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОперацияВФонеЗавершения", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыводИмеющихсяАктовВСписокДоговоров()
	
	Объект.СписокДоговоров.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	ВКМ_СозданныеАктыУслуг.Договор КАК Договор,
		|	ВКМ_СозданныеАктыУслуг.Ссылка КАК Ссылка
		|ИЗ
		|	РегистрНакопления.ВКМ_СозданныеАктыУслуг КАК ВКМ_СозданныеАктыУслуг
		|ГДЕ
		|	ВКМ_СозданныеАктыУслуг.Период >= &ДатаНачала
		|	И ВКМ_СозданныеАктыУслуг.Период <= &ДатаОкончания";
	
	Запрос.УстановитьПараметр("ДатаНачала", Объект.Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Объект.Период.ДатаОкончания);
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.СписокДоговоров.Добавить();
		НоваяСтрока.Договор = Выборка.Договор;
		НоваяСтрока.Ссылка = Выборка.Ссылка;
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Функция СоздатьАктыНаСервере()
		
	НаименованиеЗадания = НСтр("ru = 'Формирование актов'");
	ВыполняемыйМетод = "Обработки.ВКМ_МассовоеСозданиеАктов.СозданныеАкты";
	ПараметрыФункции = Новый Структура;
	ПараметрыФункции.Вставить("Период", Объект.Период);
	ПарамертыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПарамертыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПарамертыВыполнения.ЗапуститьВФоне = Истина;
	ПарамертыВыполнения.Вставить("ИдентификаторФормы", УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, ВыполняемыйМетод, ПараметрыФункции);

КонецФункции

&НаКлиенте
Процедура ОперацияВФонеЗавершения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		ДлительныеОперацииЗавершениеНаСервере(Результат.АдресРезультата);
		ОбщегоНазначенияКлиент.СообщитьПользователю("Формирование документов реализации выполнено успешно");
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Статус операции: Ошибка выполнения, обратитесь к системному администратору");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДлительныеОперацииЗавершениеНаСервере(АдресХранилища)
	
	ДанныеСозданныхДокументов = ПолучитьИзВременногоХранилища(АдресХранилища);
	УдалитьИзВременногоХранилища(АдресХранилища);
	
	Для Каждого Строка Из ДанныеСозданныхДокументов Цикл
		НоваяСтрока = Объект.СписокДоговоров.Добавить();
		НоваяСтрока.Договор = Строка.Договор;
		НоваяСтрока.Ссылка = Строка.Ссылка;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти
