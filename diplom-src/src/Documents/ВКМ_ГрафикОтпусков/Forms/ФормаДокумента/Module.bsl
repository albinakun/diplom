
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		ПроверкаОтпусковЗаГод();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников

&НаКлиенте
Процедура ОтпускаСотрудниковДатаНачалаПриИзменении(Элемент)
	ПроверкаОтпусковЗаГод();
КонецПроцедуры

&НаКлиенте
Процедура ОтпускаСотрудниковДатаОкончанияПриИзменении(Элемент)
	ПроверкаОтпусковЗаГод();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АнализГрафика(Команда)
	
	АдресВременногоХранилища = ПоместитьДанныеВоВременноеХранилище();
	
	ПараметрыФормы = Новый Структура("АдресВременногоХранилища", АдресВременногоХранилища);
	ОткрытьФорму("Документ.ВКМ_ГрафикОтпусков.Форма.АнализГрафика", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПоместитьДанныеВоВременноеХранилище()
	
	ДанныеТаблицы = Объект.ОтпускаСотрудников.Выгрузить();
	Адрес = ПоместитьВоВременноеХранилище(ДанныеТаблицы);
	
	Возврат Адрес;
	
КонецФункции

&НаСервере
Процедура ПроверкаОтпусковЗаГод()
	
	Записать();
	УсловноеОформление.Элементы.Очистить();
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник КАК Сотрудник,
	|	СУММА(РАЗНОСТЬДАТ(ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала,
	|		ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания, День) + 1) КАК ДнейОтпуска
	|ИЗ
	|	Документ.ВКМ_ГрафикОтпусков.ОтпускаСотрудников КАК ВКМ_ГрафикОтпусковОтпускаСотрудников
	|ГДЕ
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		НовыйЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
		ОформлениеЦветФона = НовыйЭлементУсловногоОформления.Оформление.Элементы.Найти("ЦветФона");
		ОформлениеЦветФона.Значение = WebЦвета.Красный;
		ОформлениеЦветФона.Использование = Истина;
			
		НастройкаОтбора = НовыйЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		НастройкаОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОтпускаСотрудников.Сотрудник");
		НастройкаОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		НастройкаОтбора.ПравоеЗначение = Выборка.Сотрудник;
		
		ОформляемоеПоле = НовыйЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ОтпускаСотрудниковСотрудник");
		
		Если Выборка.ДнейОтпуска > 28 Тогда		
			ОформляемоеПоле.Использование = Истина;
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("У сотрудника %1 общее количество дней отпуска составляет: %2", Выборка.Сотрудник, Выборка.ДнейОтпуска));
		Иначе
			ОформляемоеПоле.Использование = Ложь;
		КонецЕсли;
	
	КонецЦикла;
						
 КонецПроцедуры

#КонецОбласти
